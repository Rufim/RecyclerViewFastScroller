apply plugin: 'com.android.library'
apply plugin: "com.jfrog.artifactory"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion as Integer
    buildToolsVersion rootProject.ext.buildToolsVersion as String

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion as Integer
        targetSdkVersion rootProject.ext.targetSdkVersion as Integer
        versionCode rootProject.ext.versionCode as Integer
        versionName rootProject.ext.versionName as String
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:support-v4:22.2.0"
    compile "com.android.support:recyclerview-v7:22.2.0"
}

def uploadScriptPropertyName = "recyclerviewfastscroller.uploadScript"

if (project.hasProperty(uploadScriptPropertyName)
        && new File((String) project.property(uploadScriptPropertyName)).exists()) {

    apply from: project.property(uploadScriptPropertyName)

    afterEvaluate {
        androidJavadocs.classpath += project.android.libraryVariants.toList().first().javaCompile.classpath
    }
}

// Variables for Artifactory publishing are stored in local gradle.properties
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "${artifactory_repokey}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications ('recyclerviewfastscroller')
        }
    }
}

publishing {
    publications {
        recyclerviewfastscroller(MavenPublication) {
            groupId 'com.brightcove'
            artifactId 'recyclerviewfastscroller'
            version rootProject.ext.versionName as String
            artifact("$buildDir/outputs/aar/recyclerviewfastscroller-release.aar")
        }
    }
}

artifactoryPublish {
    dependsOn assemble
}
